module RecType(F : ★ ➔ ★).

import top.
import cast.

RecFunctor ◂ ★ =
  ∀ X : ★ . ∀ Y : ★ . Cast · X · Y ➔ Cast · (F · X) · (F · Y) .

Rec ◂ ★ = ∀ X : ★ . Cast · (F · X) · X ➾ X . 

recCast ◂ ∀ X : ★ . Cast · (F · X) · X ➾ Cast · Rec · X =
  Λ X . Λ c . [ λ d . d · X -c , β ].

recIn ◂ RecFunctor ➾ Cast · (F · Rec) · Rec =
  Λ fmap . 
    [ λ x . Λ X . Λ c .
      cast -c (cast -(fmap (recCast -c)) x),
      β ].

recOut ◂ RecFunctor ➾ Cast · Rec · (F · Rec) =
  Λ fmap . [λ x . x -(fmap (recIn -fmap)) , β ].

recFold ◂ RecFunctor ➾ Cast · (F · Rec) · Rec = recIn .
recUnfold ◂ RecFunctor ➾ Cast · Rec · (F · Rec) = recOut .

recIso1 ◂ ∀ a : Top . {recFold (recUnfold a) ≃ a} = Λ a . β.
recIso2 ◂ ∀ a : Top . {recUnfold (recFold a) ≃ a} = Λ a . β.

recFmap ◂ ∀ X: ★. ∀ Y: ★. RecFunctor ➾ Cast · X · Y ➾ F · X ➔ F · Y =
  Λ X . Λ Y . Λ f . Λ c . cast -(f c).
